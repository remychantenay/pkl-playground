@go.Package { name = "github.com/remychantenay/pkl-playground/internal/config" }
module remychantenay.pkl_playground.internal.config

import "package://pkg.pkl-lang.org/pkl-go/pkl.golang@0.8.0#/go.pkl"

/// SomeInteger is just a simple integer.
someInteger: Int = 42

/// SomeString is a simple string.
someString: String = "42"

/// Kind is an "enum" type.
typealias Kind = "floating" | "fixed"

// Window is a class that holds window information.
class Window {
  /// Width is the width of the entire window.
  width: Float(this >= 1400.0) = 1400.0

  /// Height is the eight of the entire window.
  height: Float(this >= 700.0) = 700.0

  /// Elevation is the elevation of the window.
  /// Nullable.
  elevation: Int? = null

  /// Kind is the kind of window.
  kind: Kind = "floating"
}

/// window contains the window configuration.
window: Window

/// Tabs contains the list of tabs.
tabs = new Listing<String> {
  "tab_one"
  "tab_two"
}

/// Components is a map of components.
components = new Mapping<String, String> {
  ["C1"] = "58c2ef10-b7c3-46e6-a757-4217c60e2ce8"
  ["C2"] = "5c819cbd-9db1-4425-98e7-ea77742a9fa9"
}

/// mult multiplies a by b.
function mult(a: Int, b: Int) =
  a * b

/// FullList is a list of integers.
fullList = List(1, 2, 3, 4, 5)

/// Multiplied contains the elements of fullList * 2.
multiplied = fullList.map((i) -> mult(i, 2))

/// Filtered contains the elements of fullList filtered.
filtered = fullList.filter((i) -> i == 3 || i == 5)